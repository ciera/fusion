/**
* JJTree file
*/

options {
  JDK_VERSION = "1.5";
  STATIC = false;
}
 
PARSER_BEGIN(FPLParser) 
package edu.cmu.cs.fusion.parsers.predicate;

import edu.cmu.cs.fusion.constraint.predicates.*;
import edu.cmu.cs.fusion.constraint.operations.*;
import java.util.*;
import edu.cmu.cs.crystal.util.Utilities;
import edu.cmu.cs.crystal.util.Pair;
import edu.cmu.cs.fusion.Relation;
import edu.cmu.cs.fusion.RelationsEnvironment;
import edu.cmu.cs.fusion.constraint.Effect;
import edu.cmu.cs.fusion.constraint.RelEffect;
import edu.cmu.cs.fusion.constraint.Operation;
import edu.cmu.cs.fusion.constraint.SpecVar;
import edu.cmu.cs.fusion.constraint.Predicate;
import java.io.StringReader;
import org.eclipse.jdt.core.IType;
import org.eclipse.jdt.core.JavaModelException;


public class FPLParser {
  	RelationsEnvironment relations;
  	IType context;
  	
	public FPLParser(RelationsEnvironment rels, IType context)	{
	  	this("", rels, context);
	}

	public FPLParser(String input, RelationsEnvironment rels, IType context)
	{
	  	this(new StringReader(input));
		this.context = context;
		this.relations = rels;
	}

	public void reset(String input)	{		ReInit(new StringReader(input));
	}
	
}
PARSER_END(FPLParser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN : /* OPERATORS */
{
	< EQUALS: "==" > 
|	< NEQ: "!=" > 
|	< INSTANCEOF: "instanceof" >
|   < NEG :"!" >
|   < TEST :"?" >
|   < TEST_SEP :":" >
|   < OR : "OR" >
|   < IMPLIES : "IMPLIES" >
|   < AND : "AND" >

}

TOKEN : /* Operation Tokens */
{
	< EOM: "EOM" > 
|   < BOM : "BOM" >
|   < NOP : "XML" >
|   < DOT : "." >
|   < WC : "*" >
|   < UPDATE : "update" >
}



TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: (< DIGIT >)+ >
|
  < TRUE: "TRUE" >
| 
  < FALSE:  "FALSE" >
| 
  <STATIC: "STATIC" >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|  < #LETTER: ["_","a"-"z","A"-"Z"] >
|  < #DIGIT: ["0"-"9"] >
}

Predicate expression():
{Predicate p;
} 
{
  p = impExpression()
  {return p;}
}

Predicate impExpression():
{
	Predicate rhs;
	Predicate lhs;
}
{
  lhs = orExpression()
  (
    ( < IMPLIES > ) rhs = orExpression()
  {
    lhs = new ImpliesPredicate(lhs, rhs);
    rhs = null;
  }
  )*
  {
    return lhs;
  }
}

Predicate orExpression():
{
	Predicate rhs;
	Predicate lhs;
}
{
  lhs = andExpression()
  (
    ( < OR > ) rhs = andExpression()
  {
    lhs = new OrPredicate(lhs, rhs);
    rhs = null;
  }
  )*
  {
    return lhs;
  }
}

Predicate andExpression():
{
	Predicate rhs;
	Predicate lhs;
}
{
  lhs = parenExpression()
  (
  ( <AND > ) rhs = parenExpression()
  {
    lhs = new AndPredicate(lhs, rhs);
    rhs = null;  }
  )*
  {    return lhs;
  }
}

Predicate parenExpression():
{
Predicate p;
}
{
  "(" p = expression() ")" { return p;}
| 
  p = relationship() { return p;}
}

Predicate relationship() :
{
Predicate p;
}
{
	p = testUserRelationship()	{ return p;}
| 
  	LOOKAHEAD(3)
	p = userRelationship() {return p;}
|
	LOOKAHEAD(2)
	p = iof() {return p;}
|
	LOOKAHEAD(2)
 	p = equal() {return p;}
|
	p = boolValue() {return p;}
|
	p = boolLiteral() {return p;}
}

TestPredicate testUserRelationship() :
{
	SpecVar test;
	boolean isPos = true;
	RelationshipPredicate inner;}
{
	< TEST >
	inner = userRelationship()
	< TEST_SEP >
	[< NEG > {isPos = false;	}]
	test = param()
	{
		return new TestPredicate(inner, test, isPos);	  
	} }

RelationshipPredicate userRelationship() :
{
	Relation rel;
	boolean isPos = true;
	SpecVar[] params;
	int numParams;
	String relName;
}
{
	[< NEG >{isPos = false;	}]
 	 rel = relation()
	{
	  numParams = rel.getFullyQualifiedTypes().length;
		relName = rel.getName();
	}
	params = params(numParams, relName)
	{
	  return new RelationshipPredicate(rel, params, isPos);	}
}

InstanceOfPredicate iof():
{
	SpecVar v;
	String qualifiedType;
	boolean isPos = true;
}
{
	v = param()
	[< NEG >{isPos = false;	}] < INSTANCEOF >
	qualifiedType = type()
	{
	  return new InstanceOfPredicate(v, qualifiedType, isPos);
	}
}

ReferenceEqualityPredicate equal():
{
	SpecVar left, right;
	boolean isEquals = true;
}
{
	left = param()	(	  < EQUALS > | < NEQ >	  {	    isEquals = false;
	  }
	)
	right = param()
	{
		return new ReferenceEqualityPredicate(left, right, isEquals);	}
}

BooleanValue boolValue() :
{
	SpecVar v;}
{	v = param()
	{	  return new BooleanValue(v, true);
	}
|
	< NEG >
	v = param()
	{		return new BooleanValue(v, false);
	}
}

/** A literal. */
Predicate boolLiteral() :
{
}
{ 
	< TRUE > { return new TruePredicate(); }|
	< FALSE > { return new FalsePredicate(); }
}



/**
*
* Parsing of Effects
*
**/
Effect effect():
{
	Effect e;
}
{
e = relEffect() {return e;} 
}

RelEffect relEffect():
{
  Relation rel;
  int numParams;
  boolean isPos = true;
  SpecVar[] vars;
  SpecVar testVar;
  String name;
}
{
  [< NEG >{isPos = false;	}]
  rel = relation()
  {
    numParams = rel.getFullyQualifiedTypes().length;
	name = rel.getName();
  }  vars = wcParams(numParams, name)
  {
    if (isPos)
    	return RelEffect.createAddEffect(rel, vars);
    else
    	return RelEffect.createRemoveEffect(rel, vars);  }
|
  <TEST >
  [< NEG >{isPos = false;	}]
  rel = relation()
 {
    numParams = rel.getFullyQualifiedTypes().length;
	name = rel.getName();
  }
  vars = wcParams(numParams, name)
  < TEST_SEP >
  testVar = param()
  {    if (isPos)
    	return RelEffect.createTestEffect(rel, vars, testVar);
    else
    	return RelEffect.createNegatedTestEffect(rel, vars, testVar); 
  }
}




/**
*
* Operation parsing
*
**/

Operation operation() :{
  Operation op;}
{
	op = eom() {return op;}
|
op = bom() {return op;}
| 
op = nop() {return op;}
|
LOOKAHEAD(2)
	op = invoke() {return op;}
|
	op = construct() {return op;}}

NoOp nop() :
{
}
{
	< NOP > {return new NoOp();}
}


EndOfMethodOp eom() :
{
  String receiverType;
  String methodName;
  Pair<String[], SpecVar[] > params;
  String resType;
}
{LOOKAHEAD(2)
	< EOM >
":"
receiverType = wcType()
< DOT >
methodName = wcName()
params = wcTypedParams()
":"
resType = wcType()
{
  return new EndOfMethodOp(receiverType, methodName, params.snd(), params.fst(), resType);
}
|
	< EOM >
	{	  return new EndOfMethodOp(null, null, null, null, null);
	}
}

BeginOfMethodOp bom() :
{
  String receiverType;
  String methodName;
  Pair<String[], SpecVar[] > params;
}
{
	LOOKAHEAD(2)
	< BOM >
":"
receiverType = wcType()
< DOT >
methodName = wcName()
params = wcTypedParams()
{
  return new BeginOfMethodOp(receiverType, methodName, params.snd(), params.fst());}
|
< BOM >	 {return new BeginOfMethodOp(null, null, null, null);}
}


ConstructorOp construct() :
{
  String receiverType;
  Pair<String[], SpecVar[] > params;
}
{
  receiverType = type()
  params = typedParams()
  {

	return new ConstructorOp(receiverType, params.snd(), params.fst());
  }
}

MethodInvocationOp invoke() :
{
  String receiverType;
  Token name;
  Pair<String[], SpecVar[] > params;
  String returnType;
  boolean isStatic = false;}
{
 [< STATIC > {isStatic = true;} ]
  receiverType = type()
  < DOT >
  name = < IDENTIFIER >
  params = typedParams()
  ":"
  returnType = type()
  {
	return new MethodInvocationOp(name.image, receiverType, params.snd(), params.fst(), returnType, isStatic);
  }}










/**
*
* General parsing utlities
*
**/




Relation relation():
{
  Token t;
  Relation rel;}
{
	t = <IDENTIFIER>
	{
	  try	  {
		  rel = relations.findRelation(Utilities.resolveType(context, t.image));
		  if (rel == null)
		  	throw new ParseException("The relation " + t.image + " was found within the context of " + context.getFullyQualifiedName() + ", but no relation existed!");
		  return rel;
		}
		catch (JavaModelException err)		{			throw new ParseException("Model error found when trying to resolve " + t.image + " within the context of " + context.getFullyQualifiedName() + ".");
		}
	}}

Pair<String[], SpecVar[]> wcTypedParams() :
{
  Pair<String[], SpecVar[]> params;
}
{
LOOKAHEAD(2)
	"("< DOT ><DOT >")" {return new Pair<String[], SpecVar[]>(null, null);}
| 
params = typedParams() {return params;} 
}

Pair<String[], SpecVar[]> typedParams() :
{
  SpecVar var;
  String type;
  ArrayList<SpecVar > params = new ArrayList<SpecVar>();
  ArrayList<String> types = new ArrayList<String>();
  String[] typeArr;
  SpecVar[] paramArr;
}
{
	"("
	[
	type = type()
	var = param()
	{
	  params.add(var);
	  types.add(type);
	}

	(
	  "," type = type() var = param()
	{
	  params.add(var);
	  types.add(type);
	}
	)*
	]
	")"
  {
    paramArr = new SpecVar[params.size()];
    typeArr = new String[params.size()];    
	for (int ndx = 0; ndx < params.size(); ndx++) {		paramArr[ndx] = params.get(ndx);
		typeArr[ndx] = types.get(ndx);
	}
    
    return new Pair<String[], SpecVar[]>(typeArr, paramArr);
  }   
}


SpecVar[] params(int numParams, String name) :{
  int ndx;
  SpecVar var;
  SpecVar[] params;}
{
	{
	  params = new SpecVar[numParams];
	  ndx = 0;
	}
	"(" var = param()
	{
	  if (ndx > numParams - 1)
	  	throw new ParseException("Too many parameters for the relation " + name + " , expected " + numParams + ".");
	  params[ndx] = var;
	  ndx++;
	}
	(
	  "," var = param()
	{
	  if (ndx > numParams - 1)
	  	throw new ParseException("Too many parameters for the relation " + name + " , expected " + numParams + ".");
	  params[ndx] = var;
	  ndx++;
	}
	)* ")"
	{
	  if (ndx < numParams - 1)
	  	throw new ParseException("Too few parameters for this relation " + name + " , expected " + numParams + ".");
	 }
  {return params;}   }

SpecVar[] wcParams(int numParams, String relName) :
{
  int ndx;
  SpecVar var;
  SpecVar[] params;
}
{
	{
	  params = new SpecVar[numParams];
	  ndx = 0;
	}
	"(" var = wcParam()
	{
	  if (ndx > numParams - 1)
	  	throw new ParseException("Too many parameters for the relation " + relName + " , expected " + numParams + ".");
	  params[ndx] = var;
	  ndx++;
	}
	(
	  "," var = wcParam()
	{
	  if (ndx > numParams - 1)
	  	throw new ParseException("Too many parameters for this relation " + relName + " , expected " + numParams + ".");
	  params[ndx] = var;
	  ndx++;
	}
	)* ")"
	{
	  if (ndx < numParams - 1)
	  	throw new ParseException("Too few parameters for this relation " + relName + " , expected " + numParams + ".");
	 }
  {return params;}   
}



String wcType() :
{
  String qualifiedType;
}
{
  < WC >
  {
    return null;
  }
| 
  qualifiedType = type()
  {
    return qualifiedType;
  }
}


String type() :
{
  Token t;
  String qualifiedType;
}
{	t = < IDENTIFIER >
	{
	  try
	  {
		  qualifiedType = Utilities.resolveType(context, t.image);
		  return qualifiedType;
	  }
	  catch (JavaModelException err)
	  {
	    throw new ParseException("The type " + t.image + " could not be found within the context of " + context.getFullyQualifiedName() + ".");
	  }
	}
}

SpecVar wcParam() :
{
  SpecVar var;
}
{
  < WC >
  {
    return SpecVar.createWildCard();
  }
| 
  var = param()
  {
    return var;
  }
}

SpecVar param() :
{
  Token t;
}
{
  t = < IDENTIFIER >
  {    return new SpecVar(t.image);
  }
}

String wcName() :
{
  Token name;
}
{
  < WC > {return null;}
| 
  name = < IDENTIFIER > {return name.image;}
}
